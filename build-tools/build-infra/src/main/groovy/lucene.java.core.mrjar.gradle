/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Produce an MR-JAR for panama vector implementations

configure(project(":lucene:core")) {
  plugins.withType(JavaPlugin).configureEach {
    mrjarJavaVersions.each { jdkVersion ->
      sourceSets.create("main${jdkVersion}") {
        java {
          srcDirs = ["src/java${jdkVersion}"]
        }
      }

      configurations["main${jdkVersion}Implementation"].extendsFrom(configurations.maybeCreate("implementation"))
      dependencies.add("main${jdkVersion}Implementation", sourceSets.main.output)

      tasks.named("compileMain${jdkVersion}Java").configure {
        def apijar = apijars.file("jdk${jdkVersion}.apijar")

        // TODO: this depends on the order of argument configuration...
        int releaseIndex = options.compilerArgs.indexOf("--release")
        options.compilerArgs.removeAt(releaseIndex)
        options.compilerArgs.removeAt(releaseIndex)

        // Remove conflicting options for the linter. #14782
        options.compilerArgs.removeAll("-Xlint:options")

        options.compilerArgs += [
          "-Xlint:-options",
          "--add-exports",
          "java.base/java.lang.foreign=ALL-UNNAMED",
          // for compilation we patch the incubator packages into java.base, this has no effect on resulting class files:
          "--add-exports",
          "java.base/jdk.incubator.vector=ALL-UNNAMED",
        ]

        def argsProvider = objects.newInstance(CompilerArgsProvider)
        argsProvider.apiJarFile.set(apijar)
        options.compilerArgumentProviders.add(argsProvider)
      }
    }

    tasks.named('jar').configure {
      boolean needMRJAR = false
      int minJavaVersion = Integer.parseInt(JavaVersion.toVersion(deps.versions.minJava.get()).majorVersion)

      mrjarJavaVersions.each { jdkVersion ->
        // the sourceSet which corresponds to the minimum/base Java version
        // will copy its output to root of JAR, all other sourceSets will go into MR-JAR folders:
        boolean isBaseVersion = (jdkVersion.toString() as int) <= minJavaVersion
        into(isBaseVersion ? '' : "META-INF/versions/${jdkVersion}") {
          from sourceSets["main${jdkVersion}"].output
        }
        needMRJAR |= !isBaseVersion
      }

      if (needMRJAR) {
        manifest.attributes(
            'Multi-Release': 'true'
            )
      }
    }
  }
}

abstract class CompilerArgsProvider implements CommandLineArgumentProvider {
  @InputFile
  @PathSensitive(PathSensitivity.RELATIVE)
  abstract RegularFileProperty getApiJarFile()

  @Override
  Iterable<String> asArguments() {
    return [
      "--patch-module",
      "java.base=${apiJarFile.get().asFile.absolutePath}".toString()
    ]
  }
}
