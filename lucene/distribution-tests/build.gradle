/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-library'
}

configurations {
  distributionModules
}

dependencies {
  implementation("com.carrotsearch.randomizedtesting:randomizedtesting-runner", {
    exclude group: "junit"
  })
  implementation("junit:junit", {
    exclude group: "org.hamcrest"
  })
  implementation "org.hamcrest:hamcrest"

  implementation "org.assertj:assertj-core"

  distributionModules project(path: ":lucene:distribution", configuration: "jars")
}

// Configure the test task so that it collects Lucene distribution modules and passes
// their location to the integration tests.
ext {
  distributionModulesDir = file("${buildDir}/distributionModules")
}

task assembleDistributionModules(type: Sync) {
  destinationDir distributionModulesDir
  from configurations.distributionModules
}

test {
  dependsOn assembleDistributionModules

  systemProperty "lucene.distribution.modules", assembleDistributionModules.destinationDir
  systemProperty "lucene.distribution.version", "${project.version}"
}
