/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Configure binary distribution content defaults for each subproject.

configure(rootProject.ext.mavenProjects) {
  plugins.withType(JavaPlugin) {
    ext {
      binaryArtifactsDir = file("${buildDir}/binaryArtifacts")
    }

    configurations {
      // All of the module's "binary" artifacts that should go to the release.
      binaryArtifacts

      // Legacy binary distribution contains Lucene artifacts (JARs) and their dependencies
      // under 'lib/'. We have to go through some hoops to split the artifact from all of its
      // transitive dependencies and separate their output location.
      transitiveDependencies {
        // All of runtime elements
        extendsFrom runtimeElements
        // Exclude cross-module dependencies
        exclude group: "org.apache.lucene"
        // Exclude the following from all projects, if they appear.
        exclude group: "commons-logging"
        exclude group: "org.slf4j"
      }
    }

    task assembleBinaryArtifacts(type: Sync) {
      from(tasks.findByName("jar"))

      from(projectDir, {
        include "README.*"
      })

      from(configurations.transitiveDependencies, {
        into "lib/"
      })

      into binaryArtifactsDir
    }

    artifacts {
      binaryArtifacts binaryArtifactsDir, {
        builtBy assembleBinaryArtifacts
      }
    }
  }
}


// Add launch scripts for Luke.
configure(project(":lucene:luke")) {
  tasks.matching { it.name == "assembleBinaryArtifacts" }.all {
    from("${projectDir}/bin", {
    })
  }
}
