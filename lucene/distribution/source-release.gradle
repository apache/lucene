/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


// Prepare the "source" distribution artifact. We use raw git export, no additional complexity needed.

configure(project(":lucene:distribution")) {
  task assembleSourceTgz() {
    description "Assemble source Lucene artifact as a .tgz file."

    // Make sure no unstaged/ dirty files are present.
    dependsOn ":gitStatus", ":checkWorkingCopyClean"

    ext {
      destination = file("${buildDir}/packages/lucene-${version}-src.tgz")
    }

    inputs.property("git-revision", { -> rootProject.ext.gitRev })
    outputs.file destination

    doFirst {
      quietExec {
        executable = project.externalTool("git")
        workingDir = project.rootDir

        args += [
            "archive",
            "--format", "tgz",
            "--prefix", "lucene-${version}/",
            "--output", destination,
            "HEAD"
        ]
      }
    }
  }
}
