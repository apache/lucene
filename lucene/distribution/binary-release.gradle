/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


// Configure Lucene's binary release. This is a bit convoluted so is placed
// in a separate script.

configure(project(":lucene:distribution")) {
  def packageBaseName = "${buildDir}/packages/lucene-${version}"

   // Prepare site documentation dependency for inclusion.
  configurations {
    docs

    jars
  }

  dependencies { DependencyHandler handler ->
    docs project(path: ':lucene:documentation', configuration: 'site')

    // Maven-published submodule JARs are part of the binary distribution
    // (with a few exceptions explicitly filtered below).
    // We don't copy their transitive dependencies.
    def binaryModules = rootProject.ext.mavenProjects.findAll { p -> !(p in [
        // Will end up in a separate binary?
        project(":lucene:luke"),
        // NOCOMMIT: Currently breaks the module system so excluded for now.
        project(":lucene:test-framework"),
    ]) }
    for (Project module : binaryModules) {
      jars(project(path: module.path), {
        transitive = false
      })
    }
  }


  task assembleBinaryTgz(type: Tar) {
    description "Assemble binary Lucene artifact as a .tgz file."

    archiveFileName = packageBaseName + ".tgz"
    destinationDirectory = file(archiveFileName).parentFile

    reproducibleFileOrder = true
    compression = Compression.GZIP

    // Manually correct posix permissions (matters when assembling archives on Windows).
    filesMatching(["**/*.sh", "**/*.bat"]) { copy ->
      copy.setMode(0755)
    }

    // Attach binary release exclusive files.
    from(file("src/binary-release"), {
    })

    // Cherry-pick certain files from the root.
    from(project(':').projectDir, {
      include "LICENSE.txt"
      include "NOTICE.txt"
    })

    // Cherry-pick certain files from the lucene module.
    from(project(':lucene').projectDir, {
      include "CHANGES.txt"
      include "JRE_VERSION_MIGRATION.md"
      include "MIGRATE.md"
      include "SYSTEM_REQUIREMENTS.md"

      include "licenses/*"
    })

    // The documentation.
    from(configurations.docs, {
      into 'docs'
    })

    // Binary artifacts from modules.
    from(configurations.jars, {
      into 'jars'
    })

    // Internal archive folder for all files.
    into "lucene-${rootProject.version}/"
  }
}
