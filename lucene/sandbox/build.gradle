/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'dev.welbyseely.gradle-cmake-plugin' version '0.0.3'
}
apply plugin: 'java-library'

description = 'Various third party contributions and new ideas'

dependencies {
  moduleImplementation files('dpu/dpu.jar')
  moduleApi project(':lucene:core')
  moduleApi project(':lucene:queries')
  moduleTestImplementation project(':lucene:test-framework')
}

cmake {
  sourceFolder = file('dpu/src')
  installPrefix = "$projectDir/dpu/build"
  generator = 'Ninja'

  buildConfig='Release'
  buildTarget='all'
  buildClean=true
}

task cmakeInstall (type: dev.welbyseely.CMakeBuildTask) {
  configureFromProject()
  buildTarget = 'install'
  inputs.files(fileTree('dpu/src'))
  outputs.files('dpu/build/libdpuLucene.so', 'dpu/build/dpu_program_exact_phrase', 'dpu/build/libdpujnilucene.so')
}

task cmakeBuildTest (type: dev.welbyseely.CMakeBuildTask) {
  configureFromProject()
  buildTarget = 'run_test'
  inputs.files(fileTree('dpu/src'))
}

cmakeInstall.dependsOn cmakeBuild
assemble.dependsOn cmakeInstall
clean.dependsOn cmakeClean

cmakeClean.doFirst {
  delete file("$projectDir/dpu/build/libdpuLucene.so")
  delete file("$projectDir/dpu/build/dpu_program_exact_phrase")
  delete file("$projectDir/dpu/build/libdpujnilucene.so")
}

test {
  systemProperty 'lucene.pim.dir', "${rootProject.projectDir}"
  systemProperty 'java.library.path', "$projectDir/dpu/build:$projectDir/dpu/"
  environment 'LD_LIBRARY_PATH', "$projectDir/dpu/build/"
}
