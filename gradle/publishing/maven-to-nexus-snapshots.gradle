/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


// Configure artifact push to apache nexus (snapshots repository, CI job)

configure(rootProject) {
  ext {
    apacheNexusSnapshotsRepository = "https://repository.apache.org/content/repositories/snapshots"
  }

  task mavenToApacheSnapshots() {
    group "Publishing"
    description "Publish Lucene Maven artifacts to Apache Snapshots repository: ${apacheNexusSnapshotsRepository}"

    dependsOn rootProject.ext.mavenProjects.collect {
      it.tasks.matching { it.name == "publishJarsPublicationToApacheSnapshotsRepository" }
    }
  }

  configure(rootProject.ext.mavenProjects) { Project project ->
    plugins.withType(PublishingPlugin) {
      publishing {
        repositories {
          maven {
            name = "ApacheSnapshots"
            url = apacheNexusSnapshotsRepository

            // These access credentials are passed via Apache CI jobs, typically.
            credentials {
              def nexusUserName = rootProject.propertyOrDefault('asfNexusUsername', null)
              def nexusPwd = rootProject.propertyOrDefault('asfNexusPassword', null)
              if (nexusUserName && nexusPwd) {
                username nexusUserName
                password nexusPwd
              }
            }
          }
        }
      }
    }
  }
}