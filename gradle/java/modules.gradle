/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Configure miscellaneous aspects required for supporting the java module system layer.

// Debugging utilities.
apply from: buildscript.sourceFile.toPath().resolveSibling("modules-debugging.gradle")

allprojects {
  plugins.withType(JavaPlugin) {
    // We won't be using gradle's built-in automatic module finder.
    java {
      modularity.inferModulePath.set(false)
    }

    // More verbose debugging for paths.
    boolean debugPaths = Boolean.parseBoolean(propertyOrDefault('build.debug.paths', 'false'))

    //
    // Configure modular extensions for each source set.
    //
    project.sourceSets.all { SourceSet sourceSet ->
      // Create and register a source set extension for manipulating classpath/ module-path
      ModularPathsExtension modularPaths = new ModularPathsExtension(project, sourceSet)
      sourceSet.extensions.add("modularPaths", modularPaths)

      // TODO: the tests of these projects currently don't compile or work in
      // module-path mode. Make the modular paths extension use class path only.
      if (sourceSet.name == "test" && project.path in [
        // Circular dependency between artifacts or source set outputs,
        // causing package split issues at runtime.
        ":lucene:core",
        ":lucene:codecs",
        ":lucene:spatial3d",
        ":lucene:spatial-extras",
        ":lucene:test-framework",
      ]) {
        modularPaths.mode = ModularPathsExtension.Mode.CLASSPATH_ONLY
      }

      // Configure the JavaCompile task associated with this source set.
      tasks.named(sourceSet.getCompileJavaTaskName()).configure({ JavaCompile task ->
        task.dependsOn modularPaths.compileModulePathConfiguration

        // LUCENE-10327: don't allow gradle to emit an empty sourcepath as it would break
        // compilation of modules.
        task.options.setSourcepath(sourceSet.java.sourceDirectories)

        // Add modular dependencies and their transitive dependencies to module path.
        task.options.compilerArgumentProviders.add(modularPaths.compilationArguments)

        // LUCENE-10304: if we modify the classpath here, IntelliJ no longer sees the dependencies as compile-time
        // dependencies, don't know why.
        if (!rootProject.ext.isIdea) {
          task.classpath = modularPaths.compilationClasspath
        }

        doFirst {
          if (debugPaths) {
            logger.lifecycle(modularPaths.compilationPathsToString())
          } else {
            logger.info(modularPaths.compilationPathsToString())
          }
        }
      })

      // For source sets that contain a module descriptor, configure a jar task that combines
      // classes and resources into a single module.
      if (modularPaths.hasModuleDescriptor()) {
        tasks.maybeCreate(sourceSet.getJarTaskName(), org.gradle.jvm.tasks.Jar).configure({
          from(sourceSet.output)
        })
      }
    }

    // Connect modular configurations between their "test" and "main" source sets, this reflects
    // the conventions set by the Java plugin.
    project.configurations {
      moduleTestApi.extendsFrom moduleApi
      moduleTestImplementation.extendsFrom moduleImplementation
      moduleTestRuntimeOnly.extendsFrom moduleRuntimeOnly
      moduleTestCompileOnly.extendsFrom moduleCompileOnly
    }

    // Gradle's java plugin sets the compile and runtime classpath to be a combination
    // of configuration dependencies and source set's outputs. For source sets with modules,
    // this leads to split class and resource folders.
    //
    // We tweak the default source set path configurations here by assembling jar task outputs
    // of the respective source set, instead of their source set output folders. We also attach
    // the main source set's jar to the modular test implementation configuration.
    SourceSet mainSourceSet = project.sourceSets.getByName(SourceSet.MAIN_SOURCE_SET_NAME)
    boolean mainIsModular = mainSourceSet.modularPaths.hasModuleDescriptor()
    SourceSet testSourceSet = project.sourceSets.getByName(SourceSet.TEST_SOURCE_SET_NAME)
    boolean testIsModular = testSourceSet.modularPaths.hasModuleDescriptor()
    if (testIsModular) {
      def jarTask = project.tasks.getByName(mainSourceSet.getJarTaskName())
      def testJarTask = project.tasks.getByName(testSourceSet.getJarTaskName())
      testSourceSet.compileClasspath = project.objects.fileCollection().from(
          jarTask.outputs,
          project.configurations.getByName(testSourceSet.getCompileClasspathConfigurationName()),
      )
      testSourceSet.runtimeClasspath = project.objects.fileCollection().from(
          jarTask.outputs,
          testJarTask.outputs,
          project.configurations.getByName(testSourceSet.getRuntimeClasspathConfigurationName()),
      )

      project.dependencies {
        moduleTestImplementation files(jarTask.outputs)
      }
    }

    //
    // Configures a Test task associated with the provided source set to use module paths.
    //
    // There is no explicit connection between source sets and test tasks so there is no way (?)
    // to do this automatically, convention-style.
    //
    // This closure can be used to configure a different task, with a different source set, should we
    // have the need for it.
    Closure<Void> configureTestTaskForSourceSet = { Test task, SourceSet sourceSet ->
      task.configure {
        ModularPathsExtension modularPaths = sourceSet.modularPaths

        dependsOn modularPaths.runtimeModulePathConfiguration

        // Add modular dependencies and their transitive dependencies to module path.
        jvmArgumentProviders.add(modularPaths.runtimeArguments)

        // Modify the default classpath.
        classpath = modularPaths.runtimeClasspath

        doFirst {
          if (debugPaths) {
            logger.lifecycle(modularPaths.runtimePathsToString())
          } else {
            logger.info(modularPaths.runtimePathsToString())
          }
        }
      }
    }

    // Lazily configure (tasks.test, sourceSets.test)
    tasks.matching { it.name == "test" }.all { Test task ->
      configureTestTaskForSourceSet(task, task.project.sourceSets.test)
    }

    // Configure module versions.
    tasks.withType(JavaCompile).configureEach { task ->
      // TODO: LUCENE-10267: workaround for gradle bug. Remove when the corresponding issue is fixed.
      task.options.compilerArgumentProviders.add((CommandLineArgumentProvider) { ->
        if (task.getClasspath().isEmpty()) {
          return ["--module-version", project.version.toString()]
        } else {
          return []
        }
      })

      task.options.javaModuleVersion.set(provider {
        return project.version.toString()
      })
    }
  }
}


//
// For a source set, create explicit configurations for declaring modular dependencies.
//
// These "modular" configurations correspond 1:1 to Gradle's conventions but have a 'module' prefix
// and a capitalized remaining part of the conventional name. For example, an 'api' configuration in
// the main source set would have a corresponding 'moduleApi' configuration for declaring modular
// dependencies.
//
// Gradle's java plugin "convention" configurations extend from their modular counterparts
// so all dependencies end up on classpath by default for backward compatibility with other
// tasks and gradle infrastructure.
//
// At the same time, we also know which dependencies (and their transitive graph of dependencies!)
// should be placed on module-path only.
//
// Note that an explicit configuration of modular dependencies also opens up the possibility of automatically
// validating whether the dependency configuration for a gradle project is consistent with the information in
// the module-info descriptor because there is a (nearly?) direct correspondence between the two:
//
// moduleApi            - 'requires transitive'
// moduleImplementation - 'requires'
// moduleCompileOnly    - 'requires static'
//
class ModularPathsExtension {
  /**
   * Determines how paths are split between module path and classpath.
   */
  enum Mode {
    /**
     * Dependencies and source set outputs are placed on classpath, even if declared on modular
     * configurations. This would be the 'default' backward-compatible mode.
     */
    CLASSPATH_ONLY,

    /**
     * Dependencies from modular configurations are placed on module path. Source set outputs
     * are placed on classpath.
     */
    DEPENDENCIES_ON_MODULE_PATH
  }

  Project project
  SourceSet sourceSet
  Configuration compileModulePathConfiguration
  Configuration runtimeModulePathConfiguration

  // The mode of splitting paths for this source set.
  Mode mode = Mode.DEPENDENCIES_ON_MODULE_PATH

  ModularPathsExtension(Project project, SourceSet sourceSet) {
    this.project = project
    this.sourceSet = sourceSet

    ConfigurationContainer configurations = project.configurations

    // Create modular configurations for gradle's java plugin convention configurations.
    Configuration moduleApi = createModuleConfigurationForConvention(sourceSet.apiConfigurationName)
    Configuration moduleImplementation = createModuleConfigurationForConvention(sourceSet.implementationConfigurationName)
    Configuration moduleRuntimeOnly = createModuleConfigurationForConvention(sourceSet.runtimeOnlyConfigurationName)
    Configuration moduleCompileOnly = createModuleConfigurationForConvention(sourceSet.compileOnlyConfigurationName)

    // Apply hierarchy relationships to modular configurations.
    moduleImplementation.extendsFrom(moduleApi)

    // This part of convention configurations seems like a very esoteric use case, leave out for now.
    // sourceSet.compileOnlyApiConfigurationName

    // We have to ensure configurations are using assembled resources and classes (jar variant) as a single
    // module can't be expanded into multiple folders.
    Closure<Void> ensureJarVariant = { Configuration c ->
      c.attributes.attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, project.objects.named(LibraryElements, LibraryElements.JAR))
    }

    // Set up compilation module path configuration combining corresponding convention configurations.
    Closure<Configuration> createResolvableModuleConfiguration = { String configurationName ->
      Configuration conventionConfiguration = configurations.maybeCreate(configurationName)
      Configuration moduleConfiguration = configurations.maybeCreate(moduleConfigurationNameFor(conventionConfiguration.name))
      moduleConfiguration.canBeConsumed(false)
      moduleConfiguration.canBeResolved(true)
      ensureJarVariant(moduleConfiguration)

      project.logger.info("Created resolvable module configuration for '${conventionConfiguration.name}': ${moduleConfiguration.name}")
      return moduleConfiguration
    }

    ensureJarVariant(configurations.maybeCreate(sourceSet.compileClasspathConfigurationName))
    ensureJarVariant(configurations.maybeCreate(sourceSet.runtimeClasspathConfigurationName))

    this.compileModulePathConfiguration = createResolvableModuleConfiguration(sourceSet.compileClasspathConfigurationName)
    compileModulePathConfiguration.extendsFrom(moduleCompileOnly, moduleImplementation)

    this.runtimeModulePathConfiguration = createResolvableModuleConfiguration(sourceSet.runtimeClasspathConfigurationName)
    runtimeModulePathConfiguration.extendsFrom(moduleRuntimeOnly, moduleImplementation)
  }

  private FileCollection getCompilationModulePath() {
    if (mode == Mode.CLASSPATH_ONLY) {
      return project.files()
    }
    return compileModulePathConfiguration
  }

  private FileCollection getRuntimeModulePath() {
    if (mode == Mode.CLASSPATH_ONLY) {
      if (hasModuleDescriptor()) {
        // The source set is itself a module.
        throw new GradleException("Source set contains a module but classpath-only" +
            " dependencies requested: ${project.path}, source set '${sourceSet.name}'")
      }

      return project.files()
    }

    return runtimeModulePathConfiguration
  }

  FileCollection getCompilationClasspath() {
    if (mode == Mode.CLASSPATH_ONLY) {
      return sourceSet.compileClasspath
    }

    // Modify the default classpath by removing anything already placed on module path.
    // Use a lazy provider to delay computation.
    project.files({ ->
      return sourceSet.compileClasspath - compileModulePathConfiguration
    })
  }

  CommandLineArgumentProvider getCompilationArguments() {
    return new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        FileCollection modulePath = ModularPathsExtension.this.compilationModulePath

        if (modulePath.isEmpty()) {
          return []
        }

        ArrayList<String> extraArgs = []
        extraArgs += ["--module-path", modulePath.join(File.pathSeparator)]

        if (!hasModuleDescriptor()) {
          // We're compiling what appears to be a non-module source set so we'll
          // bring everything on module path in the resolution graph,
          // otherwise modular dependencies wouldn't be part of the resolved module graph and this
          // would result in class-not-found compilation problems.
          extraArgs += ["--add-modules", "ALL-MODULE-PATH"]
        }

        return extraArgs
      }
    }
  }

  FileCollection getRuntimeClasspath() {
    if (mode == Mode.CLASSPATH_ONLY) {
      return sourceSet.runtimeClasspath
    }

    // Modify the default classpath by removing anything already placed on module path.
    // Use a lazy provider to delay computation.
    project.files({ ->
      return sourceSet.runtimeClasspath - runtimeModulePath
    })
  }

  CommandLineArgumentProvider getRuntimeArguments() {
    return new CommandLineArgumentProvider() {
      @Override
      Iterable<String> asArguments() {
        FileCollection modulePath = ModularPathsExtension.this.runtimeModulePath

        if (modulePath.isEmpty()) {
          return []
        }

        def extraArgs = []

        // Add source set outputs to module path.
        extraArgs += ["--module-path", modulePath.files.join(File.pathSeparator)]

        // Ideally, we should only add the sourceset's module here, everything else would be resolved via the
        // module descriptor. But this would require parsing the module descriptor and may cause JVM version conflicts
        // so keeping it simple.
        extraArgs += ["--add-modules", "ALL-MODULE-PATH"]

        return extraArgs
      }
    }
  }

  boolean hasModuleDescriptor() {
    return sourceSet.allJava.srcDirs.stream()
        .map(dir -> new File(dir, "module-info.java"))
        .anyMatch(file -> file.exists())
  }

  private static String toList(FileCollection files) {
    return files.isEmpty() ? " [empty]" : ("\n    " + files.sort().join("\n    "))
  }

  String compilationPathsToString() {
    return "Modular extension, compilation paths, source set=${sourceSet.name}${hasModuleDescriptor() ? " (module)" : ""}, mode=${mode}:\n" +
        "  Module path:${toList(compilationModulePath)}\n" +
        "  Class path: ${toList(compilationClasspath)}"
  }

  String runtimePathsToString() {
    return "Modular extension, runtime paths, source set=${sourceSet.name}${hasModuleDescriptor() ? " (module)" : ""}, mode=${mode}:\n" +
        "  Module path:${toList(runtimeModulePath)}\n" +
        "  Class path: ${toList(runtimeClasspath)}"
  }

  // Map convention configuration names to "modular" corresponding configurations.
  static String moduleConfigurationNameFor(String configurationName) {
    return "module" + configurationName.capitalize().replace("Classpath", "Path")
  }

  // Create module configuration for the corresponding convention configuration.
  private Configuration createModuleConfigurationForConvention(String configurationName) {
    ConfigurationContainer configurations = project.configurations
    Configuration conventionConfiguration = configurations.maybeCreate(configurationName)
    Configuration moduleConfiguration = configurations.maybeCreate(moduleConfigurationNameFor(configurationName))
    moduleConfiguration.canBeConsumed(false)
    moduleConfiguration.canBeResolved(false)
    conventionConfiguration.extendsFrom(moduleConfiguration)

    project.logger.info("Created module configuration for '${conventionConfiguration.name}': ${moduleConfiguration.name}")
    return moduleConfiguration
  }
}